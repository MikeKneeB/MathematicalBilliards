set grid

set term pdf

if (fname[:4] eq 'frac') {
	print "Phase plot distance travelled against initial velocity angle."
	set output 'distanceVelocityAngle.pdf' 
	set ylabel "Initial velocity angle"
	set xlabel "Distance travelled"
	plot fname using 'pLength':'angle' with points pt 0 title 'mod distance'

	print "Phase plot x/y distance travelled against velocity angle."
	set output 'coordDistanceVelocityAngle.pdf' 
	plot fname using 'xLength':'angle' with points pt 0 title 'x distance', fname using 'yLength':'angle' with points pt 0 title 'y distance'

	print "Not very useful, but hopefully pretty fractal."
	set output 'radialDistanceAngle.pdf'
	plot fname using 'xVec':'yVec' with points pt 0 title 'x/y'

	exit
}

if (fname[:4] eq 'chao') {
	set output 'chaoticAnalysisLog.pdf'
	set logscale
	set xlabel "log iterations"
	set ylabel "log position difference (epsilon)"
	plot fname using 'i':'dpa' with lines title 'position difference'

	set output 'chaoticAnalysis.pdf'
	unset logscale
	set xlabel "iterations"
	set ylabel "position difference (epsilon)"
	plot fname using 'i':'dpa' title 'position difference' 

	set output 'chaoticPaths.pdf'
	set xlabel 'x'
	set ylabel 'y'
	plot fname using '1x':'1y' with lines title '1', fname using '2x':'2y' with lines title '2' 

	exit
}

if (fname[:3] eq 'box') {
	set output 'boxDim.pdf'
	set logscale
	set xlabel "log 1/boxlength"
	set ylabel "log number of occupied squares"
	plot fname using 'iboxlen':'count' with lines

	#set output 'boxDim2.pdf'
	#unset logscale
	#set xlabel "box length"
	#set ylabel "box dimension"
	#plot fname using 'boxlen':'boxdim' with lines
	
	exit
} 

	set size ratio -1 
	print 'Plotting x against y (ball path).'
	set output 'path.pdf'
	set ylabel "y"
	set xlabel "x"
	plot fname using 'x':'y' with lines title 'x y'

	print 'Plotting vx against vy.'
	set output 'vPath.pdf'
	set ylabel "vy"
	set xlabel "vx"
	plot fname using 'vx':'vy' with lines title 'vx vy'

	print 'Plotting modulus of position against angle between velocity and surface normal.'
	set output 'modPositionAngleIncidence.pdf'
	set ylabel "angle incidence"
	set xlabel "modulus position"
	plot fname every ::1 using 'mp':'a' with lines title 'mp a'

	print 'Plotting iteration no. against angle between v and s.n.'
	set output 'iterationAngleIncidence.pdf'
	set ylabel "angle incidence"
	set xlabel "iteration no."
	plot fname using 'i':'a' with lines title 'i a'

	print 'Plotting mod position against velocity angle.'
	set output 'mPosVelAngle.pdf'
	set ylabel "velocity angle"
	set xlabel "modulus position"
	plot fname every ::1 using 'mp':'va' with lines title 'mp va'

	print 'Plotting position angle against velocity angle.'
	set output 'pAngleVAngle.pdf'
	set ylabel "velocity angle"
	set xlabel "position angle"
	plot fname every ::1 using 'pa':'va' with lines title 'pa va'

	set output 'iPAngle.pdf'
	set ylabel 'position angle'
	set xlabel 'iteration no.'
	plot fname using 'i':'pa' with lines title 'i pa'

	set output 'incPAngle.pdf'
	set ylabel 'angle inicidence'
	set xlabel 'poisition angle'
	plot fname using 'a':'pa' with lines title 'a pa'

	print 'Plotting x against vx and y against vy.'
	set output 'xVxyVy.pdf'
	set ylabel "velocity"
	set xlabel "position"
	plot fname using 'x':'vx' with lines title 'x vx', fname using 'y':'vy' with lines title 'y vy'

